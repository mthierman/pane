add_library(pane)
add_library(pane::pane ALIAS pane)
target_compile_features(pane PUBLIC c_std_17 cxx_std_23)
target_compile_definitions(
    pane
    PUBLIC UNICODE WIN32_LEAN_AND_MEAN NOMINMAX GDIPVER=0x0110
)
target_compile_options(
    pane
    PUBLIC
        $<$<CXX_COMPILER_ID:MSVC>:
        /W4
        /WX
        /utf-8
        /bigobj
        /diagnostics:color
        /permissive-
        /Zc:__cplusplus,enumTypes,templateScope,throwingNew,preprocessor
        >
        $<$<AND:$<CXX_COMPILER_ID:Clang>,$<CXX_COMPILER_FRONTEND_VARIANT:MSVC>>:
        /W4
        /WX
        -Wno-braced-scalar-init
        >
        $<$<AND:$<CXX_COMPILER_ID:Clang>,$<CXX_COMPILER_FRONTEND_VARIANT:GNU>>:
        -Wall
        -Werror
        -Wno-braced-scalar-init
        >
)
target_link_options(
    pane
    PUBLIC
        $<$<CXX_COMPILER_ID:MSVC>:
        /WX
        >
        $<$<AND:$<CXX_COMPILER_ID:Clang>,$<CXX_COMPILER_FRONTEND_VARIANT:MSVC>>:
        /WX
        >
        $<$<AND:$<CXX_COMPILER_ID:Clang>,$<CXX_COMPILER_FRONTEND_VARIANT:GNU>>:
        -Wl,/WX
        >
)
target_sources(
    pane
    PUBLIC FILE_SET HEADERS BASE_DIRS "include"
    PRIVATE
        "internal/co_init.cpp"
        "internal/color.cpp"
        "internal/config.cpp"
        "internal/console.cpp"
        "internal/debug.cpp"
        "internal/error.cpp"
        "internal/file_picker.cpp"
        "internal/filesystem.cpp"
        "internal/gdi_plus.cpp"
        "internal/guid.cpp"
        "internal/hstring.cpp"
        "internal/icu.cpp"
        "internal/input.cpp"
        "internal/math.cpp"
        "internal/message_box.cpp"
        "internal/process.cpp"
        "internal/string.cpp"
        "internal/system.cpp"
        "internal/text.cpp"
        "internal/url.cpp"
        "internal/webview.cpp"
        "internal/window.cpp"
)
target_link_libraries(
    pane
    PUBLIC dwmapi gdiplus Urlmon WindowsApp ada glaze webview2 wil cppwinrt
)
